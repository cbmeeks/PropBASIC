' This program reads the distance from a PING sensor connected to pin 2.
' Converts the value to tenths of inches and sends the distance to the PC.
'
DEVICE P8X32A, XTAL1, PLL16X
FREQ 80_000_000

Baud      CON "T115200" ' Baud rate to communicate with PC

PingPin   PIN 2 LOW     ' Connected to Sig pin on Ping module
TX        PIN 30 HIGH   ' Send data back to PC

value     VAR LONG

Message  DATA "Distance is "
valueStr DATA "1234.5 inches.", 13, 0

PROGRAM Start

Start:
  DO
    PAUSE 10
    PULSOUT PingPin, 5             ' Trigger PING
    PAUSEUS 5
    PULSIN PingPin, 1, value       ' Measure PING pulse
    value = value ** 291200017     ' Convert to tenths of inches (multiply by 0.0678002873901278)
    valueStr = STR value, 5, 5     ' Convert value to ASCII
    RDBYTE valueStr(4), value      ' Insert decimal point
    WRBYTE valueStr(4), ".", value
    SEROUT TX, Baud, Message
  LOOP
END

